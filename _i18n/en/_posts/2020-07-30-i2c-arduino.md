---
layout: post
title: The I2C Bus with Arduino
date: 2020-07-30
categories: Arduino
---

The I2C-Bus was developed by Philips in the early ‘80s to provide a way to communicate several integrated circuits on the same board. The name is an abbreviation for Inter-Integrated Circuit and can also be referred as IIC or I<sup>2</sup>C. Since it was originally a trademark, other companies developed similar protocols, like Atmel with the Two Wire Interface (or TWI).

<div class="notification is-info is-light">
Note that, although <strong>TWI</strong> and <strong>I2C</strong> are <strong>different protocols</strong>, they are almost identical except some details like 10 bit addressing. For that, for most uses <strong>they are compatible</strong>.
</div>

<figure>
  <img src="/assets/images/posts/arduino-i2c/I2C_bus_en.png" title="I2C Structure">
  <figcaption>I2C Structure</figcaption>
</figure>


It’s a **synchronous** protocol, which means that it needs a clock signal to coordinate its operations, and follows a **master-slave** architecture, which means that one device[^1] coordinates up every action and the other devices follow its lead. For this, the bus only needs two wires: **SDA** (or Serial Data) and **SCL** (or Serial Clock), as seen in the image above.

<div class="notification is-info is-light">
The <strong>clock</strong> can be set at different speeds. The standard speed can be set with up to 100 kHz. For the devices which require faster speeds, fastmode reaches 400 kHz and high speed mode up to 3.4 MHz.
</div>

## Data Structure

Data is sent through the SDA and SCL wires following the next structure:

Every data packet starts with a `START` condition. This is defined by a transition from `HIGH` to `LOW` on SDA while SCL stays `HIGH`, and it's written by the master. 

Next, it comes the **address** of the slave, which must be unique for each slave. For this, 7 bits are usually used, making a total of **128 possible addresses** of which **16 are reserved for special purposes**, remaining **112 for actual devices**. This number can be improved using 10-bit addressing but since this is rarely used it won’t be covered here. The address is followed by the ***data direction*** bit, being zero a transmission (to `WRITE` onto the slave’s memory) and a one a request (to `READ` the slave’s memory).

After the address byte, it comes the **acknowledge** bit. For this bit, the master releases the SDA line for the slave to operate on it. If the slave receives the information properly it pulls to `LOW` the line and it keeps it stable during a clock pulse, meaning the byte is ***Acknowledged***. If the slave sets the line to `HIGH` it means that the previous byte is ***Not Acknowledged***. This can be caused for several reasons, including that the receiver is unable to transmit or receive any information (due to an interruption on the device, for example) or that the receiver does not understand the received information. In the case of a ***Not Acknowledged*** message, the transmission can be ended by sending a `STOP` condition or restarted by sending another `START` condition.

The next bytes are the actual message. Depending on the device, there may be more addresses, this time specifying the slave’s memory register which will be read or written. After each byte, an acknowledge bit is sent through the line. This acknowledge bits are written by the receiver of the information (the slave if we are writing or the master if we are reading).

After the last byte and acknowledge bit the data packet is ended by a `STOP` condition, defined in a similar way to the `START` condition. While the clock line is `HIGH`, the SDA line is pulled from `LOW` to `HIGH`.

The next image is an example of a message transmitted through the Bus.

<figure>
  <img src="/assets/images/posts/arduino-i2c/I2C_data_structure_en.png" title="I2C Data Structure">
  <figcaption>I2C Data Structure</figcaption>
</figure>

## I2C with Arduino

**Arduino** boards, as they are based on Atmel microcontrollers, include **support for TWI** communication, which, as seen above, is **compatible with I2C**.

The SDA and SCL pins of the Atmel chip are wired to **two physical pins** of the Arduino. These two pins vary from one model to another as seen on the table below.


| Board | SDA Pin | SCL Pin |
|-------|---------|---------|
| UNO   | A4      | A5      |
| NANO  | A4      | A5      |
| MEGA  | 20      | 21      |


There is also the possibility to use other pins for I2C communication using libraries, but, as this is via software, the maximum speed is slower.

To reduce and simplify the code needed to interact with the I2C bus, the Arduino Standard Libraries include the [Wire library](https://www.arduino.cc/en/reference/wire).

<div class="notification is-info is-light">
Besides the Wire library, there are other I2C libraries available online, such as <a href="http://www.i2cdevlib.com/"><strong>I2Cdevlib</strong></a>, which we will be using with the <strong>MPU-6050</strong> on future posts.
</div>

The basic use of the library goes as follows:

The I2C Bus must be initiated. This is done by `Wire.begin()` and must be called only once. Then, depending on whether we want to write or read from the slave's memory, we can take two paths.

If we want to write, the transmission with the device at the specified address is started by `Wire.beginTransmission(address)`. Then the bytes are queued by `Wire.write()` and wait for being written when the transmission is stopped by `Wire.endTransmission()`.

If we want to read, we must use `Wire.requestFrom(address, number_of_bytes)`. This will queue the specified number of bytes and end the transmission. The read bytes can be accessed one by one via `Wire.read()`.

As an example of use for the Wire library, we will code an **I2C scanner**, useful for when we don't know the address of the device we want to connect to, which usually happens when working with cheaper clone products as they don't have an extensive documentation. It will be a simplified form of [this script](https://playground.arduino.cc/Main/I2cScanner/).

The program will search through all the possible addresses, trying to establish a communication and finishing it right after. If a device is found, the address is printed to the Arduino serial monitor. You can **download the source code <a href="/assets/downloadables/posts/i2c-arduino/I2C_scanning.ino" download>here</a>** or copy the code from below.


{% highlight C++ linenos %}
#include <Wire.h>

void setup() {
  Wire.begin();

  Serial.begin(9600);

  Serial.println("\nI2C Scanner\n");

}

void loop() {
  uint8_t error, address;
  int num;

  Serial.println("Scanning...");

  num = 0;

  for(address = 1; address < 127; address++){

    Wire.beginTransmission(address);
    error = Wire.endTransmission();

    if (error == 0)
    {
      Serial.print("I2C device found at address 0x");
      if (address<16)
        Serial.print("0");
      Serial.println(address,HEX);
 
      num++;
    }
    else if (error==4)
    {
      Serial.print("Unknown error at address 0x");
      if (address<16)
        Serial.print("0");
      Serial.println(address,HEX);
    }    
  }
  if (num == 0)
    Serial.println("No I2C devices found\n");
  else
    Serial.println("done\n");
 
  delay(5000); 
    
}
{% endhighlight %}

## Further Reading

For more and deeper information, you can check the sources I used to write this post.

- [I2C-Bus.org](https://www.i2c-bus.org/)
- [I2C-bus spec and user manual by NXP](https://www.nxp.com/docs/en/user-guide/UM10204.pdf)

---

[^1]: In fact, it has support for multiple master devices, but it won’t be covered on this post. Check [this page](https://www.i2c-bus.org/multimaster/) for more information. 
